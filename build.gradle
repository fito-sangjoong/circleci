buildscript {
  ext {
     springBootVersion = '2.3.0.RELEASE'
  }
  repositories {
     mavenCentral()
  }
  dependencies {
     classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
  }
}

allprojects {
  group = 'scenario.circleci'
  version = '0.0.1-SNAPSHOT'
}

subprojects {
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'java'

  sourceCompatibility = '1.8'

  repositories {
     mavenCentral()
  }

  dependencies {
     compileOnly 'org.projectlombok:lombok'
     implementation 'org.springframework.boot:spring-boot-devtools'
     annotationProcessor 'org.projectlombok:lombok'
     testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
     }
  }

  test {
     useJUnitPlatform()
  }

  task initSourceFolders {
     sourceSets*.java.srcDirs*.each {
        if (!it.exists()) {
           it.mkdirs()
        }
     }
     sourceSets*.resources.srcDirs*.each {
        if (!it.exists()) {
           it.mkdirs()
        }
     }
  }
}

project(':core') {
  dependencies {
     compile('com.h2database:h2')
  }
}

project(':api') {
  dependencies {
     compile project(':core')
     implementation 'org.springframework.boot:spring-boot-starter-web'
  }
}
